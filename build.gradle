plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'com.gradle.plugin-publish' version '0.12.0'

    id 'org.cadixdev.licenser' version '0.5.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.github.joschi.licenser'
version = '0.6.1-SNAPSHOT'
description = 'A simple license header manager for Gradle (fork of org.cadixdev.licenser)'

repositories {
    gradlePluginPortal()
}

sourceSets {
    functionalTest {
    }
}

configurations {
    functionalTestImplementation.extendsFrom(testImplementation)
    functionalTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}
gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.register('functionalTest', Test) {
    description = 'Runs the functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    mustRunAfter test
}

dependencies {
    implementation gradleApi()

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    functionalTestImplementation gradleTestKit()
}

test {
    useJUnit()
}

tasks.named('check') {
    dependsOn(functionalTest)
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    plugins {
        licenserPlugin {
            id = 'com.github.joschi.licenser'
            displayName = 'licenser'
            description = project.description
            implementationClass = 'org.cadixdev.gradle.licenser.Licenser'
        }
    }
}

pluginBundle {
    website = 'https://github.com/joschi/licenser'
    vcsUrl = 'https://github.com/joschi/licenser'
    tags = ['license', 'license-header']
}
